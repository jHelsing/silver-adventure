plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'jacoco' // Uses default $buildDir/reports/jacoco
    id 'org.sonarqube' version '3.3'
    id 'com.github.spotbugs' version '5.0.0-beta.6'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'io.cucumber:cucumber-java:7.0.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    reports {
        xml.required  = true
        csv.required = false
    }
    dependsOn test // Tests are required to run before generating the report
}

sonarqube {
    properties {
        property "sonar.projectKey", "jHelsing_silver-adventure"
        property "sonar.organization", "jhelsing-1"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/*'
        property 'sonar.java.spotbugs.reportPaths', 'build/reports/spotbugs/*'
        property 'sonar.java.checkstyle.reportPaths', 'build/reports/checkstyle/*'
    }
}

spotbugsMain {
    reports {
        xml.enabled(true)
        xml.destination(file("$buildDir/reports/spotbugs/spotbugs.xml"))
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.destination(file("$buildDir/reports/checkstyle/checkstyle.xml"))
        xml.enabled(true)
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/test/resources/features']
        }
    }
}